[ -z "$NVM_DIR" ] && export NVM_DIR="$HOME/.nvm"

# Lazy-load NVM to avoid ~300ms shell startup penalty
_nvm_lazy_load() {
  unfunction nvm node npm npx 2>/dev/null
  [ -f "$NVM_DIR/nvm.sh" ] && source "$NVM_DIR/nvm.sh"

  # Set up auto-switch for .nvmrc files after NVM is loaded
  autoload -U add-zsh-hook

  load-nvmrc() {
    local nvmrc_path
    nvmrc_path="$(nvm_find_nvmrc)"

    if [ -n "$nvmrc_path" ]; then
      local nvmrc_node_version
      nvmrc_node_version=$(nvm version "$(cat "${nvmrc_path}")")

      if [ "$nvmrc_node_version" = "N/A" ]; then
        nvm install
      elif [ "$nvmrc_node_version" != "$(nvm version)" ]; then
        nvm use
      fi
    elif [ -n "$(PWD=$OLDPWD nvm_find_nvmrc)" ] && [ "$(nvm version)" != "$(nvm version default)" ]; then
      echo "Reverting to nvm default version"
      nvm use default
    fi
  }

  add-zsh-hook chpwd load-nvmrc
  load-nvmrc
}

# Define lazy wrapper functions
nvm() { _nvm_lazy_load; nvm "$@"; }
node() { _nvm_lazy_load; node "$@"; }
npm() { _nvm_lazy_load; npm "$@"; }
npx() { _nvm_lazy_load; npx "$@"; }
